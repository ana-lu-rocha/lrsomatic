process BCFTOOLS_ISEC {
    tag "${meta.id}"
    label 'process_medium'

    conda "${moduleDir}/environment.yml"
    container "${ workflow.containerEngine in ['singularity', 'apptainer'] && !task.ext.singularity_pull_docker_container ?
        'https://depot.galaxyproject.org/singularity/bcftools:1.24--h118bc1c_2' :
        'quay.io/biocontainers/bcftools:1.24--h118bc1c_2' }"

    input:
    tuple val(meta), path(vcfs), path(tbis), path(file_list), path(targets_file), path(regions_file)

    output:
    tuple val(meta), path("${prefix}", type: "dir"), emit: results
    tuple val(meta), path("${prefix}/0002.vcf.gz"), emit: deepvar_consensus_vcf
    tuple val(meta), path("${prefix}/0002.vcf.gz.tbi"), emit: deepvar_consensus_tbi
    tuple val(meta), path("${prefix}/0003.vcf.gz"), emit: clair_consensus_vcf
    tuple val(meta), path("${prefix}/0003.vcf.gz.tbi"), emit: clair_consensus_tbi
    tuple val(meta), path("${prefix}/0001.vcf.gz"), emit: clair_private_vcf
    tuple val(meta), path("${prefix}/0001.vcf.gz.tbi"), emit: clair_private_tbi
    tuple val(meta), path("${prefix}/0000.vcf.gz"), emit: deepvar_private_vcf
    tuple val(meta), path("${prefix}/0000.vcf.gz.tbi"), emit: deepvar_private_tbi

    tuple val("${task.process}"), val('bcftools'), eval("bcftools --version | sed '1!d; s/^.*bcftools //'"), topic: versions, emit: versions_bcftools

    when:
    task.ext.when == null || task.ext.when

    script:
    def args = task.ext.args ?: ''
    prefix = task.ext.prefix ?: "${meta.id}"
    targets_file_args = targets_file ? "-T ${targets_file}" : ''
    regions_file_args = regions_file ? "-R ${regions_file}" : ''
    vcf_files = file_list ? "-l ${file_list}" : "${vcfs}"

    """
    bcftools isec  \\
        ${args} \\
        ${targets_file_args} \\
        ${regions_file_args} \\
        -p ${prefix} \\
        ${vcf_files}
    """

    stub:
    prefix = task.ext.prefix ?: "${meta.id}"
    """
    mkdir ${prefix}
    touch ${prefix}/README.txt
    touch ${prefix}/sites.txt
    echo "" | gzip > ${prefix}/0000.vcf.gz
    touch ${prefix}/0000.vcf.gz.tbi
    echo "" | gzip > ${prefix}/0001.vcf.gz
    touch ${prefix}/0001.vcf.gz.tbi
    echo "" | gzip > ${prefix}/0002.vcf.gz
    touch ${prefix}/0002.vcf.gz.tbi
    echo "" | gzip > ${prefix}/0003.vcf.gz
    touch ${prefix}/0003.vcf.gz.tbi
    """
}
