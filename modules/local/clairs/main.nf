process CLAIRS {
    tag "$meta.id"
    label 'process_very_high'

    container "${ workflow.containerEngine in ['singularity', 'apptainer'] && !task.ext.singularity_pull_docker_container ?
        'docker.io/hkubal/clairs:v0.5.1' :
        'docker.io/hkubal/clairs:v0.5.1' }"

    input:
    tuple val(meta), path(tumor_bam), path(tumor_bai), path(normal_bam), path(normal_bai), val(model)
    tuple val(meta2), path(reference)
    tuple val(meta3), path(index)

    output:
    tuple val(meta), path("*.vcf.gz"),               emit: vcfs
    tuple val(meta), path("*.vcf.gz.tbi"),           emit: tbi
    tuple val("${task.process}"), val('clairs'), eval("/opt/bin/run_clairs  --version |& sed '1!d ; s/run_clairs //'"), topic: versions, emit: versions_clairs

    when:
    task.ext.when == null || task.ext.when

    script:
    def prefix = task.ext.prefix ?: "${meta.id}"
    def args = task.ext.args ?: ''

    """
    /opt/bin/run_clairs \
        --tumor_bam_fn $tumor_bam \\
        --normal_bam_fn $normal_bam \\
        --ref_fn $reference \\
        --threads $task.cpus \\
        --platform $model \\
        --sample_name ${prefix} \\
        --output_dir . \\
        --output_prefix snvs \\
        $args

    if [[ -f "snv.vcf.gz" ]]; then
        rm snv.vcf.gz
        rm snv.vcf.gz.tbi
    fi
    """

    stub:
    """
    echo "" | gzip > snvs.vcf.gz
    touch snvs.vcf.gz.tbi

    echo "" | gzip > indel.vcf.gz
    touch indel.vcf.gz.tbi
    """
}
